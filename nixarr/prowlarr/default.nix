{
  config,
  lib,
  pkgs,
  ...
}:
with lib; let
  cfg = config.nixarr.prowlarr;
  globals = config.util-nixarr.globals;
  nixarr = config.nixarr;
  port = 9696;
in {
  options.nixarr.prowlarr = {
    enable = mkOption {
      type = types.bool;
      default = false;
      example = true;
      description = ''
        Whether or not to enable the Prowlarr service. This has
        a seperate service since running two instances is the standard
        way of being able to query both ebooks and audiobooks.
      '';
    };

    package = mkPackageOption pkgs "prowlarr" {};

    port = mkOption {
      type = types.port;
      default = port;
      description = "Port for Prowlarr to use.";
    };

    stateDir = mkOption {
      type = types.path;
      default = "${nixarr.stateDir}/prowlarr";
      defaultText = literalExpression ''"''${nixarr.stateDir}/prowlarr"'';
      example = "/nixarr/.state/prowlarr";
      description = ''
        The location of the state directory for the Prowlarr service.

        > **Warning:** Setting this to any path, where the subpath is not
        > owned by root, will fail! For example:
        >
        > ```nix
        >   stateDir = /home/user/nixarr/.state/prowlarr
        > ```
        >
        > Is not supported, because `/home/user` is owned by `user`.
      '';
    };

    openFirewall = mkOption {
      type = types.bool;
      defaultText = literalExpression ''!nixarr.prowlarr.vpn.enable'';
      default = !cfg.vpn.enable;
      example = true;
      description = "Open firewall for Prowlarr";
    };

    vpn.enable = mkOption {
      type = types.bool;
      default = false;
      example = true;
      description = ''
        **Required options:** [`nixarr.vpn.enable`](#nixarr.vpn.enable)

        Route Prowlarr traffic through the VPN.
      '';
    };

    sonarr.configuration = mkOption {
      type = types.attrs;
      default = {
        syncCategories = [
          5000
          5010
          5020
          5030
          5040
          5045
          5050
          5090
        ];
        animeSyncCategories = [5070];
        syncAnimeStandardFormatSearch = true;
        syncRejectBlocklistedTorrentHashesWhileGrabbing = true;
      };

      description = ''
        configuration applied to the autogenerated prowlarr sonarr integeration
      '';
    };
    radarr.configuration = mkOption {
      type = types.attrs;
      default = {
        syncCategories = [
          2000
          2010
          2020
          2030
          2040
          2045
          2050
          2060
          2070
          2080
          2090
        ];
        syncRejectBlocklistedTorrentHashesWhileGrabbing = true;
      };

      description = ''
        configuration applied to the autogenerated prowlarr radarr integeration
      '';
    };
  };

  config = mkIf (nixarr.enable && cfg.enable) {
    assertions = [
      {
        assertion = cfg.vpn.enable -> nixarr.vpn.enable;
        message = ''
          The nixarr.prowlarr.vpn.enable option requires the
          nixarr.vpn.enable option to be set, but it was not.
        '';
      }
    ];

    systemd.tmpfiles.rules = [
      "d '${cfg.stateDir}' 0700 ${globals.prowlarr.user} root - -"
    ];

    services.prowlarr = {
      enable = cfg.enable;
      package = cfg.package;
      settings.server.port = cfg.port;
      openFirewall = cfg.openFirewall;
    };

    systemd.services.prowlarr.serviceConfig = {
      # `User` and `Group` override `DynamicUser = true` from the NixOS Prowlarr
      # module (because a user and group with those names exists).
      User = globals.prowlarr.user;
      Group = globals.prowlarr.group;
      ExecStart = mkForce "${lib.getExe cfg.package} -nobrowser -data=${cfg.stateDir}";
      ReadWritePaths = [cfg.stateDir];
    };

    systemd.services.prowlarr-setup = {
      description = "Setup Prowlarr environment";
      requires =
        ["prowlarr.service"]
        ++ (optionals nixarr.radarr.enable ["radarr.service" "radarr-api-key.service"])
        ++ (optionals nixarr.sonarr.enable ["sonarr.service" "sonarr-api-key.service"]);
      after =
        ["prowlarr.service"]
        ++ (optionals nixarr.radarr.enable ["radarr.service" "radarr-api-key.service"])
        ++ (optionals nixarr.sonarr.enable ["sonarr.service" "sonarr-api-key.service"]);

      serviceConfig = {
        Type = "oneshot";
        RemainAfterExit = true;
        UMask = "0077"; # Results in 0600 permissions
        User = config.services.prowlarr.user;
        ExecStart = let
          sonarr_part = optionalString nixarr.sonarr.enable ''
            sonarr_api_key = open(sonarr_api_key_path, "r").read()
            sonarr = {
              "prowlarrUrl": "http://localhost:${builtins.toString cfg.port}",
              "baseUrl": "http://localhost:${builtins.toString nixarr.sonarr.port}",
              "apiKey": sonarr_api_key,
            } | json.loads('${builtins.toJSON cfg.sonarr.configuration}')
            data.append(("nixarr_autosync_sonarr", "Sonarr", json.dumps(sonarr), "SonarrSettings", 2, "[]"))
          '';
          radarr_part = optionalString nixarr.radarr.enable ''
            radarr_api_key = open(radarr_api_key_path, "r").read()
            radarr = {
              "prowlarrUrl": "http://localhost:${builtins.toString cfg.port}",
              "baseUrl": "http://localhost:${builtins.toString nixarr.radarr.port}",
              "apiKey": radarr_api_key,
            } | json.loads('${builtins.toJSON cfg.radarr.configuration}')
            data.append(("nixarr_autosync_radarr", "Radarr", json.dumps(radarr), "RadarrSettings", 2, "[]"))
          '';
        in
          pkgs.writers.writePython3Bin "prowlarr-setup" {
            libraries = [];
          } ''
            import sqlite3
            import json
            import os.path
            import time
            state_dir = "${cfg.stateDir}"
            radarr_api_key_path = state_dir + "/api-keys/radarr.key"
            sonarr_api_key_path = state_dir + "/api-keys/sonarr.key"
            db_path = state_dir + "/prowlarr.db"
            while not os.path.exists(db_path):
                time.sleep(1)
            data = []
            ${sonarr_part}
            ${radarr_part}
            con = sqlite3.connect(db_path)
            cur = con.cursor()
            cur.executemany("INSERT INTO Applications VALUES(NULL, ?, ?, ?, ?, ?, ?) ON CONFLICT(Name) DO UPDATE SET Settings=excluded.Settings", data)
            con.commit()
          '';
      };
    };

    networking.firewall = mkIf cfg.openFirewall {
      allowedTCPPorts = [cfg.port];
    };

    users = {
      groups.${globals.prowlarr.group}.gid = globals.gids.${globals.prowlarr.group};
      users.${globals.prowlarr.user} = {
        isSystemUser = true;
        group = globals.prowlarr.group;
        uid = globals.uids.${globals.prowlarr.user};
      };
    };

    # Enable and specify VPN namespace to confine service in.
    systemd.services.prowlarr.vpnConfinement = mkIf cfg.vpn.enable {
      enable = true;
      vpnNamespace = "wg";
    };

    vpnNamespaces.wg = mkIf cfg.vpn.enable {
      portMappings = [
        {
          from = cfg.port;
          to = cfg.port;
        }
      ];
    };
    services.nginx = mkIf cfg.vpn.enable {
      enable = true;

      recommendedTlsSettings = true;
      recommendedOptimisation = true;
      recommendedGzipSettings = true;

      virtualHosts."127.0.0.1:${builtins.toString cfg.port}" = {
        listen = [
          {
            addr = "0.0.0.0";
            port = cfg.port;
          }
        ];
        locations."/" = {
          recommendedProxySettings = true;
          proxyWebsockets = true;
          proxyPass = "http://192.168.15.1:${builtins.toString cfg.port}";
        };
      };
    };
  };
}
